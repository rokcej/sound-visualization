<!DOCTYPE html>
<html>
<head>
	<title>Sound Visualization</title>

	<meta charset="UTF-8">

	<link rel="icon" type="image/png" href="data/favicon.png"/>
	<link rel="stylesheet" type="text/css" href="src/css/style.css">

	<!-- <script src="lib/three.js"></script> -->
	<script type="module" src="src/main.js" ></script>


	<script type="x-shader/x-vertex" id="ball_vert">
#define M_PI    3.141592653589793238462643
#define M_SQRT2 1.414213562373095048801689

uniform float time;
uniform float beat;
uniform float avg;
uniform int beat_idx;

out vec4 vColor;


// Xorshift random number generator
// https://en.wikipedia.org/wiki/Xorshift
uint xorshift32_state = 0u;
uint xorshift32(uint s) {
	s ^= (s << 13u);
	s ^= (s >> 17u);
	s ^= (s << 5u);
	return s;
}
float rand() {
	xorshift32_state = xorshift32(xorshift32_state);
	return float(xorshift32_state) / float(0xFFFFFFFFu);
}
void srand(float seed) {
	xorshift32_state = uint(seed * float(0xFFFFFFFFu));
}

// Map random number to 3D position
// http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0/
float rand_constant(vec3 xyz) { // Edited for 3D
    return fract(sin(mod(dot(xyz.xyz, vec3(12.9898, 78.233, 5.1337)), 3.14)) * 43758.5453);
}



////////////////////////////
// RNG-based Perlin noise //
////////////////////////////

// Random unit-length vector
vec3 getGradient3D(vec3 ixyz) {
	srand(rand_constant(ixyz + 1.0)); // +1 to avoid 0

	// Uniform sampling of unit sphere surface
	// http://corysimon.github.io/articles/uniformdistn-on-sphere/
	float cosTheta = 1.0 - 2.0 * rand();
	float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
	//float theta = acos(1.0 - 2.0 * rand());
	float phi = 2.0 * M_PI * rand();
	return vec3(
		sinTheta * cos(phi),
		sinTheta * sin(phi),
		cosTheta
	);
}

// Interpolation function, has zero first and second derivative at grid nodes
vec3 fade(vec3 x) {
	return x * x * x * (x * (x * 6.0 - 15.0) + 10.0); // 6x^5 - 15x^4 + 10x^3
}

// Internal Perlin noise functions
float _perlinNoise3D(vec3 p) {
	// Integer position
	vec3 ip = floor(p);
	// Difference in position
	vec3 dp = fract(p);
	// Interpolation parameter
	vec3 t = fade(dp);
	// Weights
	float w000 = dot(getGradient3D(ip + vec3(0., 0., 0.)), dp - vec3(0., 0., 0.));
	float w100 = dot(getGradient3D(ip + vec3(1., 0., 0.)), dp - vec3(1., 0., 0.));
	float w010 = dot(getGradient3D(ip + vec3(0., 1., 0.)), dp - vec3(0., 1., 0.));
	float w110 = dot(getGradient3D(ip + vec3(1., 1., 0.)), dp - vec3(1., 1., 0.));
	float w001 = dot(getGradient3D(ip + vec3(0., 0., 1.)), dp - vec3(0., 0., 1.));
	float w101 = dot(getGradient3D(ip + vec3(1., 0., 1.)), dp - vec3(1., 0., 1.));
	float w011 = dot(getGradient3D(ip + vec3(0., 1., 1.)), dp - vec3(0., 1., 1.));
	float w111 = dot(getGradient3D(ip + vec3(1., 1., 1.)), dp - vec3(1., 1., 1.));
	// Interpolate weights
	vec4 w_x = mix(vec4(w000, w001, w010, w011), vec4(w100, w101, w110, w111), t.x);
	vec2 w_xy = mix(w_x.xy,	w_x.zw,	t.y);
	float w_xyz = mix(w_xy.x, w_xy.y, t.z);
	
	// https://www.gamedev.net/forums/topic/285533-2d-perlin-noise-gradient-noise-range--/#entry2794056
	// Scale output to range [-1, 1]
	return w_xyz * M_SQRT2; // That's how Ken Perlin does it
}

// Callable Perlin noise functions
float perlinNoise3D(vec3 xyz, int octaves) {
	float octaveAmplitude = 1.0;
	float octaveFrequency = 1.0;

	float noiseSum = 0.0;
	float amplitudeSum = 0.0;
	for (int o = 0; o < octaves; ++o) {
		noiseSum += octaveAmplitude * _perlinNoise3D(xyz * vec3(vec2(octaveFrequency), 1.0));
		amplitudeSum += octaveAmplitude;

		octaveAmplitude *= 0.5; // uPersistence;
		octaveFrequency *= 2.0; // uLacunarity;
	}
	noiseSum /= amplitudeSum; // Normalize noise back to [-1, 1]

	return clamp((noiseSum + 1.0) * 0.5, 0.0, 1.0); // Convert to range [0, 1]
}

void main() {
	float noise = perlinNoise3D(position + time * 0.0, 1);

	float scale = 1.5 + 3.5 * noise * avg;

	vec4 mvPosition = modelViewMatrix * vec4(position * scale, 1.0 );

	gl_Position = projectionMatrix * mvPosition;

	float dist = length(mvPosition);
	float opacity = 1.0 - clamp((dist - 3.0) / 8.0, 0.0, 1.0);


	opacity *= 0.6 * beat + 0.4;

	vColor = vec4(0.75, 0.15, 0.9, opacity);
	//vColor = vec4(vec3(uv.x * uv.y), 1.0);
}
	</script>

	<script type="x-shader/x-fragment" id="ball_frag">
in vec4 vColor;

void main() {

	gl_FragColor = vColor;
}
	</script>

	<script type="x-shader/x-fragment" id="ball2_frag">
in vec4 vColor;

void main() {

	gl_FragColor = vec4(vColor.rgb, vColor.a * 0.333);
}
	</script>


	<script type="x-shader/x-vertex" id="particles_vert">		
#define M_PI    3.141592653589793238462643
#define M_SQRT2 1.414213562373095048801689

uniform sampler2D specTex;
uniform sampler2D avgTex;
uniform float specOff;
uniform vec2 uvDim;

uniform float time;

out vec4 vColor;



// Xorshift random number generator
// https://en.wikipedia.org/wiki/Xorshift
uint xorshift32_state = 0u;
uint xorshift32(uint s) {
	s ^= (s << 13u);
	s ^= (s >> 17u);
	s ^= (s << 5u);
	return s;
}
float rand() {
	xorshift32_state = xorshift32(xorshift32_state);
	return float(xorshift32_state) / float(0xFFFFFFFFu);
}
void srand(float seed) {
	xorshift32_state = uint(seed * float(0xFFFFFFFFu));
}

// Map random number to 3D position
// http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0/
float rand_constant(vec3 xyz) { // Edited for 3D
    return fract(sin(mod(dot(xyz.xyz, vec3(12.9898, 78.233, 5.1337)), 3.14)) * 43758.5453);
}



////////////////////////////
// RNG-based Perlin noise //
////////////////////////////


// Random unit-length vector
vec3 getGradient3D(vec3 ixyz) {
	srand(rand_constant(ixyz + 1.0)); // +1 to avoid 0

	// Uniform sampling of unit sphere surface
	// http://corysimon.github.io/articles/uniformdistn-on-sphere/
	float cosTheta = 1.0 - 2.0 * rand();
	float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
	//float theta = acos(1.0 - 2.0 * rand());
	float phi = 2.0 * M_PI * rand();
	return vec3(
		sinTheta * cos(phi),
		sinTheta * sin(phi),
		cosTheta
	);
}

// Interpolation function, has zero first and second derivative at grid nodes
vec3 fade(vec3 x) {
	return x * x * x * (x * (x * 6.0 - 15.0) + 10.0); // 6x^5 - 15x^4 + 10x^3
}

// Internal Perlin noise functions
float _perlinNoise3D(vec3 p) {
	// Integer position
	vec3 ip = floor(p);
	// Difference in position
	vec3 dp = fract(p);
	// Interpolation parameter
	vec3 t = fade(dp);
	// Weights
	float w000 = dot(getGradient3D(ip + vec3(0., 0., 0.)), dp - vec3(0., 0., 0.));
	float w100 = dot(getGradient3D(ip + vec3(1., 0., 0.)), dp - vec3(1., 0., 0.));
	float w010 = dot(getGradient3D(ip + vec3(0., 1., 0.)), dp - vec3(0., 1., 0.));
	float w110 = dot(getGradient3D(ip + vec3(1., 1., 0.)), dp - vec3(1., 1., 0.));
	float w001 = dot(getGradient3D(ip + vec3(0., 0., 1.)), dp - vec3(0., 0., 1.));
	float w101 = dot(getGradient3D(ip + vec3(1., 0., 1.)), dp - vec3(1., 0., 1.));
	float w011 = dot(getGradient3D(ip + vec3(0., 1., 1.)), dp - vec3(0., 1., 1.));
	float w111 = dot(getGradient3D(ip + vec3(1., 1., 1.)), dp - vec3(1., 1., 1.));
	// Interpolate weights
	vec4 w_x = mix(vec4(w000, w001, w010, w011), vec4(w100, w101, w110, w111), t.x);
	vec2 w_xy = mix(w_x.xy,	w_x.zw,	t.y);
	float w_xyz = mix(w_xy.x, w_xy.y, t.z);
	
	// https://www.gamedev.net/forums/topic/285533-2d-perlin-noise-gradient-noise-range--/#entry2794056
	// Scale output to range [-1, 1]
	return w_xyz * M_SQRT2; // That's how Ken Perlin does it
}

// Callable Perlin noise functions
float perlinNoise3D(vec3 xyz, int octaves) {
	float octaveAmplitude = 1.0;
	float octaveFrequency = 1.0;

	float noiseSum = 0.0;
	float amplitudeSum = 0.0;
	for (int o = 0; o < octaves; ++o) {
		noiseSum += octaveAmplitude * _perlinNoise3D(xyz * vec3(vec2(octaveFrequency), 1.0));
		amplitudeSum += octaveAmplitude;

		octaveAmplitude *= 0.5; // uPersistence;
		octaveFrequency *= 2.0; // uLacunarity;
	}
	noiseSum /= amplitudeSum; // Normalize noise back to [-1, 1]

	return clamp((noiseSum + 1.0) * 0.5, 0.0, 1.0); // Convert to range [0, 1]
}


void main() {

	vec3 pos = vec3((uv.y - 0.5) * 80.0, 0.0, (uv.x - 1.0) * 64.0 + 2.0);
	pos += position;

	vec2 uvMirrored = vec2(uv.x, uv.y * 2.0);
	if (uvMirrored.y > 1.0)
		uvMirrored.y = 2.0 - uvMirrored.y;
	uvMirrored.y = 1.0 - uvMirrored.y;

	//uvMirrored.y = (uvMirrored.y * 31.0 + 0.5) / 32.0;
	uvMirrored = (uvMirrored * (uvDim - 1.0) + 0.5) / uvDim;

	float off = perlinNoise3D(vec3(vec2(pos.x, mod(pos.z + specOff * 64.0, 64.0)) * 0.1, time * 0.0), 6);
	float power = texture2D(specTex, uvMirrored + vec2(specOff, 0.0)).r;
	pos.y += off * 6.0 * power;


	vec4 mvPosition = modelViewMatrix * vec4( pos, 1.0 );


	float pointSize = 10.0;
	float projSize = pointSize / -mvPosition.z;

	// vec4 projSize4 = projectionMatrix * vec4(pointSize, 0.0, mvPosition.zw);
	// float projSize = projSize4.x / projSize4.w;

	float opacity = 1.0;
	if (projSize < 1.0)
		opacity *= projSize * projSize;

	gl_PointSize = projSize;

	gl_Position = projectionMatrix * mvPosition;

	float powerPrev = texture2D(specTex, uvMirrored + vec2(specOff, 0.0) - 1.0 / uvDim).r;
	float diff = power - powerPrev;

	float avg = texture2D(avgTex, vec2(uvMirrored.x + specOff - 1.0 / uvDim.x, 0.5)).r;

	opacity *= (avg * 0.5 + 0.5);

	vColor = vec4(min(abs(diff) + avg, 1.0), power * 0.5 + 0.5, min(1.0 - (power * 0.5 + 0.5) + avg, 1.0), opacity);
}
	</script>

	<script type="x-shader/x-fragment" id="particles_frag">
in vec4 vColor;

void main() {
	gl_FragColor = vColor;
}
	</script>


</head>
<body>
	<div>
		<canvas id="canvas" width="1600" height="900"></canvas>
	</div>
	<div>
		<audio id="audio" src="data/kda_villain.mp3" controls></audio>
	</div>
	<div>
		<canvas id="spectrogram"></canvas>
	</div>
</body>
</html>
